buildscript {
    ext.production = (findProperty('prod') ?: 'false') == 'true'

    repositories {
        jcenter()
        maven { url = 'https://dl.bintray.com/kotlin/kotlin-eap' }
        maven { url = 'https://plugins.gradle.org/m2/' }
        maven { url = 'https://kotlin.bintray.com/kotlinx' }

    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"
        classpath "org.jetbrains.kotlin:kotlin-serialization:${kotlinVersion}"
        classpath "org.jetbrains.kotlin:kotlin-frontend-plugin:${frontendPluginVersion}"
        classpath 'org.hidetake:gradle-ssh-plugin:2.10.1'
        classpath "gradle.plugin.net.octyl:ts2kt-unofficial-gradle-plugin-plugin:0.2.0"
    }
}

plugins {
    id "com.moowork.grunt" version "1.2.0"
}

apply plugin: 'kotlin2js'

apply plugin: 'org.hidetake.ssh'

if (production) {
    apply plugin: 'kotlin-dce-js'
}
apply plugin: 'org.jetbrains.kotlin.frontend'
apply plugin: 'kotlinx-serialization'
apply plugin: "net.octyl.ts2kt-unofficial"


repositories {
    jcenter()
    maven { url = 'https://dl.bintray.com/kotlin/kotlin-eap' }
    maven { url = 'https://kotlin.bintray.com/kotlinx' }
    maven { url = 'https://dl.bintray.com/kotlin/kotlin-js-wrappers' }
    maven { url = 'https://dl.bintray.com/gbaldeck/kotlin' }
    maven { url = 'https://dl.bintray.com/rjaros/kotlin' }
    mavenLocal()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-js:${kotlinVersion}"
    compile "org.jetbrains.kotlinx:kotlinx-html-js:0.6.12"
    compile "org.jetbrains:kotlin-react:16.6.0-pre.70-kotlin-1.3.21"
    compile "org.jetbrains:kotlin-react-dom:16.6.0-pre.70-kotlin-1.3.21"
    compile 'org.jetbrains:kotlin-styled:1.0.0-pre.70-kotlin-1.3.21'
    compile "org.jetbrains.kotlinx:kotlinx-serialization-runtime-js:0.11.0"

    testCompile "org.jetbrains.kotlin:kotlin-test-js:${kotlinVersion}"
}

remotes {
    webServer {
        host = '192.168.1.151'
        user = 'pi'
        password = '726915'
    }
}

ssh.settings {
    knownHosts = allowAnyHosts
}
task deploy {
    doLast {
        ssh.run {
            session(remotes.webServer) {
                put from: buildDir.path + "/distributions/" + project.name, into: '/home/pi/Desktop/dist'
                execute 'uname'
            }
        }
    }
}

kotlinFrontend {

    npm{
        dependency 'react'
        dependency 'react-dom'
        dependency 'uglifyjs-webpack-plugin'
        dependency "inline-style-prefixer"
        dependency "url-loader"
        dependency "botbuilder"
        dependency "react-flip-move"
        dependency "messaging-api-telegram"
        dependency "firebase"
        dependency("styled-components","^3.4.10")
        devDependency "@jetbrains/ts2kt-automator"
    }

    webpackBundle {
        bundleName = "main"
        contentPath = file('src/main/web')
        mode = production ? "production" : "development"
        port = 9000
    }

    define "PRODUCTION", production

}

compileKotlin2Js {
    kotlinOptions.metaInfo = true
    kotlinOptions.outputFile = "$project.buildDir.path/js/${project.name}.js"
    kotlinOptions.sourceMap = !production
    kotlinOptions.moduleKind = 'commonjs'
}

compileTestKotlin2Js {
    kotlinOptions.metaInfo = true
    kotlinOptions.outputFile = "$project.buildDir.path/js-tests/${project.name}-tests.js"
    kotlinOptions.sourceMap = !production
    kotlinOptions.moduleKind = 'commonjs'
}

task pot(type: GruntTask) {
    args = ["pot"]
}

task po2json(type: GruntTask) {
    args = ["default"]
    if (file('translation').isDirectory()) {
        inputs.dir(file('translation'))
        outputs.dir(file('build/js'))
        outputs.dir(file('build/kotlin-js-min/main'))
    }
}

pot.dependsOn 'installGrunt'
pot.dependsOn 'npmInstall'
po2json.dependsOn 'installGrunt'
po2json.dependsOn 'npmInstall'

task copyResources(type: Copy) {
    from "src/main/resources"
    into file(buildDir.path + "/js")
}

task copyResourcesForDce() {
    doLast {
        copy {
            from "src/main/resources"
            ext.modulesDir = new File("${buildDir.path}/node_modules_imported/")
            modulesDir.eachDir {
                if (it.name.startsWith("kvision")) {
                    from(it) {
                        include "css/**"
                        include "img/**"
                        include "js/**"
                    }
                }
            }
            into file(buildDir.path + "/kotlin-js-min/main")
        }
    }
}

task dist(type: Copy, dependsOn: 'bundle') {
    from "src/main/web"
    from "${buildDir.path}/bundle"
    from "src/main/resources"
    into file(buildDir.path + "/distributions/" + project.name)
}

task distZip(type: Zip, dependsOn: 'dist') {
    from(buildDir.path + "/distributions/" + project.name)
}

afterEvaluate {
    if (production) {
        tasks.getByName("copyResourcesForDce") { dependsOn(runDceKotlinJs) }
    }
    tasks.getByName("webpack-bundle") {
        dependsOn(po2json, copyResources, copyResourcesForDce)
    }
    tasks.getByName("webpack-run") { dependsOn(po2json, copyResources) }
    tasks.getByName("karma-start") { dependsOn(po2json, copyResources) }
}
